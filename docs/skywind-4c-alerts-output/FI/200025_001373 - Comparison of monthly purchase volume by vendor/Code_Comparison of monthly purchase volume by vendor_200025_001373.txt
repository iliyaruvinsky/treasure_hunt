FUNCTION ZABC4C_F_SW_10_07_CM_MN_VL_CV.
*"--------------------------------------------------------------------
*"*"Local Interface:
*"  EXPORTING
*"     VALUE(IS_ALERT) TYPE  CHAR1
*"  TABLES
*"      T_SELECT STRUCTURE  RSSELECT OPTIONAL
*"      T_DATA STRUCTURE  /ABC4C/S_SW_10_07_CM_MN_VL_CV OPTIONAL
*"--------------------------------------------------------------------
*-----------------------------------------------------------------------
*  Date(dd-mmm-yyyy): 29-Jul-2019
*  Author           : Taotsu Nakamura - 50023710
*-----------------------------------------------------------------------
*  Alert Definition : This alert detects exceptional sales(purchase) volume increase (decrease)
*                     by comparing fiscal periods between current and last year.
*                     It could be checked on customer(vendor) level.
*-----------------------------------------------------------------------
*  CHANGE HISTORY
* ----------------------------------------------------------------------
*  DATE(DMY)     TP Request#   Programmer   Description
*  29-Jul-2019   SD3K900118    50023710     Create initial version
*  04-Aug-2019   SD3K900124    50023710     Correction by QA process
* ----------------------------------------------------------------------
* ----------------------------------------------------------------------
* Local Type definition
* ----------------------------------------------------------------------
  TYPES:
    BEGIN OF TYP_FIG,
      ACCT_NUM   TYPE /ABC4C/E_SW_ACCT_NUM,         " Account Number
      BUKRS      TYPE BUKRS,                        " Company Code
      GJAHR      TYPE GJAHR,                        " Fiscal Year
      UM01U      TYPE UMXXU,                        " Sales in the Posting Period
      UM02U      TYPE UMXXU,                        " Sales in the Posting Period
      UM03U      TYPE UMXXU,                        " Sales in the Posting Period
      UM04U      TYPE UMXXU,                        " Sales in the Posting Period
      UM05U      TYPE UMXXU,                        " Sales in the Posting Period
      UM06U      TYPE UMXXU,                        " Sales in the Posting Period
      UM07U      TYPE UMXXU,                        " Sales in the Posting Period
      UM08U      TYPE UMXXU,                        " Sales in the Posting Period
      UM09U      TYPE UMXXU,                        " Sales in the Posting Period
      UM10U      TYPE UMXXU,                        " Sales in the Posting Period
      UM11U      TYPE UMXXU,                        " Sales in the Posting Period
      UM12U      TYPE UMXXU,                        " Sales in the Posting Period
    END   OF TYP_FIG.
* ----------------------------------------------------------------------
* Local Data definition
* ----------------------------------------------------------------------
* - single DATA
  DATA_SINGLE:
    LANGU                LANGU,                    " Language (not in use)
    DATUM                SY-DATUM,                 " System Date
    TARGET               SY-DATUM,                 " Target Date
    COMPARE              SY-DATUM,                 " Date to Compare
    FNAME                FNAME,                    " Field name
    TABIX                SY-TABIX                  " table index
    .
* - range DATA
*  data_multy:
*    datum                datum,                    " Date
*    budat                budat,                    " Posting Date
*    aedat                aedat,                    " Date on Which Record Was Created
*    cpudt                cpudt,                    " Day On Which Accounting Document Was Entered
*    upddt                upddt,                    " Date of the Last Document Update
*    bldat                bldat                     " Document Date
*    .
  DATA:
    LS_DATA              TYPE /ABC4C/S_SW_10_07_CM_MN_VL_CV,
    LS_FIG               TYPE TYP_FIG,
    LT_FIG               TYPE TABLE OF TYP_FIG
    .
  FIELD-SYMBOLS:
    <FS_DATA>            TYPE /ABC4C/S_SW_10_07_CM_MN_VL_CV,
    <FS_UMXXU>           TYPE UMXXU
    .
* ----------------------------------------------------------------------
* Parameters Definition
* ----------------------------------------------------------------------
* Define Special Parameters
* - single DATA
  DATA_SINGLE:
    SW_DEST              RFCDEST,                  " RFC destination
    BACKDAYS             INT4,                     " BACKDAYS
    DATE_REF_FLD         NAME_FELD,                " DATE_REF_FLD
    DURATION_UNIT        /SKN/E_SW_DURATION_UNIT   " Duration unit
    .
* - range DATA
  DATA_MULTY:
    DURATION             /SKN/E_SW_DURATION        " Duration
    .
* Define EI specific Parameters
* - single DATA
  DATA_SINGLE:
    BACKMONTHS           /ABC4C/E_SW_BACKMONTHS,   " Months Backwards
    COMPMONTHS           /ABC4C/E_SW_COMPMONTHS,   " Months to Compare
    DC_IND               /ABC4C/E_SW_DC_IND,       " Debtor/Creditor Indicator
    PERC_VARI            /ABC4C/E_SW_PERC_VARI     " Percent Variance of fiscal period transaction volumes
    .
* - range DATA
  DATA_MULTY:
    ACCT_GRP             /ABC4C/E_SW_ACCT_GRP,     " Account Group
    ACCT_NUM             /ABC4C/E_SW_ACCT_NUM,     " Account Number
    BUKRS                BUKRS,                    " Company Code
    KNA1_LOEVM           LOEVM_X,                  " Central deletion flag
    KNA1_CASSD           CASSD_X,                  " Central sales block
    KNA1_SPERR           SPERB_X,                  " Central posting block
    KNB1_LOEVM           LOEVM_B,                  " Delete flag for company code
    KNB1_SPERR           SPERB_B,                  " Posting block for company code
    LFA1_LOEVM           LOEVM_X,                  " Central deletion flag
    LFA1_SPERM           CASSD_X,                  " Central purchasing block
    LFA1_SPERR           SPERB_X,                  " Central posting block
    LFB1_LOEVM           LOEVM_B,                  " Delete flag for company code
    LFB1_SPERR           SPERB_B                   " Posting block for company code
    .
* ----------------------------------------------------------------------
* Extracting parametersâ€™ value and populating variables
* ----------------------------------------------------------------------
* Set initial value
  LV_DATUM               = SY-DATUM." System date
  LV_LANGU               = 'E'.     " English
  LV_DATE_REF_FLD        = 'BUDAT'. " Document date
  LV_BACKMONTHS          = 1.       " Months Backwards
  LV_COMPMONTHS          = 12.      " Months to Compare
  LV_DC_IND              = 'D'.     " Debtor/Creditor Indicator
* Extract Special Parameters
* - single value
  SELECT_SINGLE:
    SW_DEST,                        " RFC destination
    BACKDAYS,                       " BACKDAYS
    DATE_REF_FLD,                   " DATE_REF_FLD
    DURATION_UNIT                   " Duration unit
    .
* - range value
  SELECT_MULTY:
    DURATION                        " Duration
    .
* Extract EI specific Parameters
* - single value
  SELECT_SINGLE:
    BACKMONTHS,                     " Months Backwards
    COMPMONTHS,                     " Months to Compare
    DC_IND,                         " Debtor/Creditor Indicator
    PERC_VARI                       " Percent Variance of fiscal period transaction volumes
    .
* - range value
  SELECT_MULTY:
    ACCT_GRP,                       " Account Group
    ACCT_NUM,                       " Account Number
    BUKRS,                          " Company Code
    KNA1_LOEVM,                     " Central deletion flag
    KNA1_CASSD,                     " Central sales block
    KNA1_SPERR,                     " Central posting block
    KNB1_LOEVM,                     " Delete flag for company code
    KNB1_SPERR,                     " Posting block for company code
    LFA1_LOEVM,                     " Central deletion flag
    LFA1_SPERM,                     " Central purchasing block
    LFA1_SPERR,                     " Central posting block
    LFB1_LOEVM,                     " Delete flag for company code
    LFB1_SPERR                      " Posting block for company code
    .
* ----------------------------------------------------------------------
* Initiating
* ----------------------------------------------------------------------
  CLEAR:
    IS_ALERT
    .
  REFRESH:
    T_DATA,
    LT_FIG
    .
* ----------------------------------------------------------------------
* Retrieving alert data
* ----------------------------------------------------------------------
  "--- Run Cloud Mode -----
  IF LV_SW_DEST IS NOT INITIAL.
    CALL FUNCTION 'ZABC4C_FC_SW_10_07_CM_MN_VL_CV'
      IMPORTING
        IS_ALERT = IS_ALERT
      TABLES
        T_SELECT = T_SELECT
        T_DATA   = T_DATA.
  ENDIF.
  CHECK LV_SW_DEST IS INITIAL.
  "--- Run Cloud Mode -----
* Calculate the Target Date from BACKMONTHS parameter.
  LV_TARGET = LV_DATUM.
  LV_TARGET+6(2) = '01'. " Get the first day of this month
  DO LV_BACKMONTHS TIMES.
    LV_TARGET = LV_TARGET - 1.
    LV_TARGET+6(2) = '01'.
  ENDDO.
* Calculate the Date to Cpmpare from COMPMONTHS parameter.
  LV_COMPARE = LV_TARGET.
  DO LV_COMPMONTHS TIMES.
    LV_COMPARE = LV_COMPARE - 1.
    LV_COMPARE+6(2) = '01'.
  ENDDO.
  IF LV_DC_IND = 'C'.
*   Extract vendor master records from Vendor Master (LFA1/LFB1).
    SELECT
            A~LIFNR AS ACCT_NUM
            A~KTOKK AS ACCT_GRP
            A~LOEVM AS LFA1_LOEVM
            A~SPERM AS LFA1_SPERM
            A~SPERR AS LFA1_SPERR
            B~BUKRS
            B~LOEVM AS LFB1_LOEVM
            B~SPERR AS LFB1_SPERR
            C~WAERS
      INTO CORRESPONDING FIELDS OF TABLE T_DATA
      FROM LFA1 AS A
      INNER JOIN LFB1 AS B ON A~LIFNR EQ B~LIFNR
      INNER JOIN T001 AS C ON B~BUKRS EQ C~BUKRS
      WHERE A~LIFNR  IN R_ACCT_NUM
      AND   A~KTOKK  IN R_ACCT_GRP
      AND   B~BUKRS  IN R_BUKRS
      AND   A~SPERR  IN R_LFA1_SPERR
      AND   A~SPERM  IN R_LFA1_SPERM
      AND   A~LOEVM  IN R_LFA1_LOEVM
      AND   B~SPERR  IN R_LFB1_SPERR
      AND   B~LOEVM  IN R_LFB1_LOEVM
      .
  ELSE.
*   Extract customer master records from Customer Master (KNA1/KNB1).
    SELECT
            A~KUNNR AS ACCT_NUM
            A~KTOKD AS ACCT_GRP
            A~LOEVM AS KNA1_LOEVM
            A~CASSD AS KNA1_CASSD
            A~SPERR AS KNA1_SPERR
            B~BUKRS
            B~LOEVM AS KNB1_LOEVM
            B~SPERR AS KNB1_SPERR
            C~WAERS
      INTO CORRESPONDING FIELDS OF TABLE T_DATA
      FROM KNA1 AS A
      INNER JOIN KNB1 AS B ON A~KUNNR EQ B~KUNNR
      INNER JOIN T001 AS C ON B~BUKRS EQ C~BUKRS
      WHERE A~KUNNR  IN R_ACCT_NUM
      AND   A~KTOKD  IN R_ACCT_GRP
      AND   B~BUKRS  IN R_BUKRS
      AND   A~SPERR  IN R_KNA1_SPERR
      AND   A~CASSD  IN R_KNA1_CASSD
      AND   A~LOEVM  IN R_KNA1_LOEVM
      AND   B~SPERR  IN R_KNB1_SPERR
      AND   B~LOEVM  IN R_KNB1_LOEVM
      .
  ENDIF.
*<<< If there is no Account Maste Data, the processing is terminated
  CHECK T_DATA[] IS NOT INITIAL.
* Calculate the fiscal year and fiscal period of Target Date
  LOOP AT T_DATA ASSIGNING <FS_DATA>.
*   Get fiscal year: Target Date
    CALL FUNCTION '/ABC4C/F_SW_10_GET_CUR_YEAR'
      EXPORTING
        BUKRS             = <FS_DATA>-BUKRS
        DATUM             = LV_TARGET
      IMPORTING
        MONAT             = <FS_DATA>-MONAT_TGT
        GJAHR             = <FS_DATA>-GJAHR_TGT
      EXCEPTIONS
        WRONG_CODE        = 1
        WRONG_VALUE       = 2
        OTHERS            = 3.
    IF SY-SUBRC <> 0 OR <FS_DATA>-GJAHR_TGT IS INITIAL.
      DELETE T_DATA INDEX SY-TABIX.
      CONTINUE.
    ENDIF.
*   Get fiscal year: Date to Compare
    CALL FUNCTION '/ABC4C/F_SW_10_GET_CUR_YEAR'
      EXPORTING
        BUKRS             = <FS_DATA>-BUKRS
        DATUM             = LV_COMPARE
      IMPORTING
        MONAT             = <FS_DATA>-MONAT_CMP
        GJAHR             = <FS_DATA>-GJAHR_CMP
      EXCEPTIONS
        WRONG_CODE        = 1
        WRONG_VALUE       = 2
        OTHERS            = 3.
    IF SY-SUBRC <> 0 OR <FS_DATA>-GJAHR_CMP IS INITIAL.
      DELETE T_DATA INDEX SY-TABIX.
      CONTINUE.
    ENDIF.
    <FS_DATA>-BACKMONTHS = LV_BACKMONTHS.
    <FS_DATA>-COMPMONTHS = LV_COMPMONTHS.
    <FS_DATA>-DC_IND     = LV_DC_IND.
  ENDLOOP.
  IF LV_DC_IND = 'C'.
*   Extract vendor transaction figures from Vendor Master (Transaction Figures) (LFC1)
    SELECT LIFNR AS ACCT_NUM BUKRS GJAHR UM01U UM02U UM03U UM04U UM05U UM06U UM07U UM08U UM09U UM10U UM11U UM12U
      INTO CORRESPONDING FIELDS OF TABLE LT_FIG
      FROM LFC1
      FOR ALL ENTRIES IN T_DATA
      WHERE LIFNR = T_DATA-ACCT_NUM
      AND   BUKRS = T_DATA-BUKRS
      AND ( GJAHR = T_DATA-GJAHR_TGT OR GJAHR = T_DATA-GJAHR_CMP ).
    SORT LT_FIG BY ACCT_NUM BUKRS GJAHR.
  ELSE.
*   Extract customer transaction figures from Customer Master (Transaction Figures) (KNC1)
    SELECT KUNNR AS ACCT_NUM BUKRS GJAHR UM01U UM02U UM03U UM04U UM05U UM06U UM07U UM08U UM09U UM10U UM11U UM12U
      INTO CORRESPONDING FIELDS OF TABLE LT_FIG
      FROM KNC1
      FOR ALL ENTRIES IN T_DATA
      WHERE KUNNR = T_DATA-ACCT_NUM
      AND   BUKRS = T_DATA-BUKRS
      AND ( GJAHR = T_DATA-GJAHR_TGT OR GJAHR = T_DATA-GJAHR_CMP ).
  ENDIF.
*<<< If there is no Figure Data, the processing is terminated
  CHECK LT_FIG[] IS NOT INITIAL.
  SORT LT_FIG BY ACCT_NUM BUKRS GJAHR.
* ----------------------------------------------------------------------
* Post retrieving manipulations
* ----------------------------------------------------------------------
*	Detect the customers(vendors) whose variances exceed the threshold(percentage) from the comparison of fiscal period transaction volumes.
  LOOP AT T_DATA ASSIGNING <FS_DATA>.
    LV_TABIX = SY-TABIX.
    CLEAR LS_FIG.
*   Get target amount
    READ TABLE LT_FIG INTO LS_FIG WITH KEY ACCT_NUM = <FS_DATA>-ACCT_NUM
                                           BUKRS    = <FS_DATA>-BUKRS
                                           GJAHR    = <FS_DATA>-GJAHR_TGT
                                           BINARY SEARCH.
    IF SY-SUBRC = 0.
      CONCATENATE 'UM' <FS_DATA>-MONAT_TGT 'U' INTO LV_FNAME.
      ASSIGN COMPONENT LV_FNAME OF STRUCTURE LS_FIG TO <FS_UMXXU>.
      <FS_DATA>-UMXXU_TGT = <FS_UMXXU>.
    ELSE.
      DELETE T_DATA INDEX LV_TABIX.
      CONTINUE.
    ENDIF.
    CLEAR LS_FIG.
*   Get compare amount
    READ TABLE LT_FIG INTO LS_FIG WITH KEY ACCT_NUM = <FS_DATA>-ACCT_NUM
                                           BUKRS    = <FS_DATA>-BUKRS
                                           GJAHR    = <FS_DATA>-GJAHR_CMP
                                           BINARY SEARCH.
    IF SY-SUBRC = 0.
      CONCATENATE 'UM' <FS_DATA>-MONAT_CMP 'U' INTO LV_FNAME.
      ASSIGN COMPONENT LV_FNAME OF STRUCTURE LS_FIG TO <FS_UMXXU>.
      <FS_DATA>-UMXXU_CMP = <FS_UMXXU>.
    ELSE.
      DELETE T_DATA INDEX LV_TABIX.
      CONTINUE.
    ENDIF.
*   Caliculate percent variance
    IF <FS_DATA>-UMXXU_TGT <> 0 AND <FS_DATA>-UMXXU_CMP <> 0.
      <FS_DATA>-PERC_VARI = <FS_DATA>-UMXXU_TGT / <FS_DATA>-UMXXU_CMP * 100 - 100.
    ENDIF.
*   Get descriptions
    IF LV_DC_IND = 'C'.
      CALL FUNCTION '/SKN/F_SW_10_VENDOR_DESC'
        EXPORTING
          LIFNR              = <FS_DATA>-ACCT_NUM
        IMPORTING
          VENDOR_DESC        = <FS_DATA>-ACCT_DESC
        EXCEPTIONS
          WRONG_VENDOR       = 1
          OTHERS             = 2.
      IF SY-SUBRC <> 0.
*       Implement suitable error handling here
      ENDIF.
    ELSE.
      CALL FUNCTION '/SKN/F_SW_10_CUST_DESC'
        EXPORTING
          KUNNR              = <FS_DATA>-ACCT_NUM
        IMPORTING
          CUST_DESC          = <FS_DATA>-ACCT_DESC
        EXCEPTIONS
          WRONG_CUSTOMER     = 1
          OTHERS             = 2.
      IF SY-SUBRC <> 0.
*       Implement suitable error handling here
      ENDIF.
    ENDIF.
*   Get descriptions
    CALL FUNCTION '/SKN/F_SW_10_COMP_CODE_DESC'
      EXPORTING
        BUKRS                = <FS_DATA>-BUKRS
      IMPORTING
        COMP_CODE_DESC       = <FS_DATA>-COMP_CODE_DESC
      EXCEPTIONS
        WRONG_CODE           = 1
        OTHERS               = 2.
    IF SY-SUBRC <> 0.
*     Implement suitable error handling here
    ENDIF.
  ENDLOOP.
* ----------------------------------------------------------------------
* Post retrieving filtering
* ----------------------------------------------------------------------
* no action
* ----------------------------------------------------------------------
* Finishing
* ----------------------------------------------------------------------
*--- Check Alert Information
  READ TABLE T_DATA INDEX 1.
  CHECK NOT SY-TFILL  IS INITIAL .
  IS_ALERT = 'X' .
ENDFUNCTION.